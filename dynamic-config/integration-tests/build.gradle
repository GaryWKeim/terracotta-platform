/*
 * Copyright (c) 2011-2019 Software AG, Darmstadt, Germany and/or Software AG USA Inc., Reston, VA, USA, and/or its subsidiaries and/or its affiliates and/or their licensors.
 * Use, reproduction, transfer, publication or disclosure is prohibited except as specifically provided for in your License Agreement with Software AG.
 */

import org.gradle.internal.jvm.Jvm

dependencies {
  testCompile project(':diagnostic-client')
  testCompile project(':dynamic-config-migration')
  testCompile project(':dynamic-config-cli')
  testCompile project(':dynamic-config-server-common')
  testCompile project(':galvan-support-ee')
  testCompile project(':terracotta-store-clustered-client')
  testCompile project(':nms-agent-entity-ee')

  testCompile "org.terracotta.management.dist:mnm-nms-agent:$terracottaPlatformVersion"
  testCompile "org.terracotta.internal:tc-config-parser:$terracottaConfigVersion"
  testCompile "org.terracotta:tcconfig-schema:$terracottaConfigVersion"
  testCompile "org.terracotta.management:monitoring-service-api:$terracottaPlatformVersion"
  testCompile "org.terracotta:offheap-resource:$terracottaPlatformVersion"
  testCompile "org.terracotta:lease-entity-server:$terracottaPlatformVersion"
  testCompile ("org.awaitility:awaitility:$awaitility")
  testCompile("com.github.stefanbirkner:system-rules:$systemRulesVersion") {
    exclude group: 'junit'
  }
}

apply plugin: TcDistribute

task unzipKit(type: Copy) {
  dependsOn project(':terracotta-db').distZip
  from zipTree(project(':terracotta-db').distZip.archivePath)
  into 'build/tc-db-kit'
}

test {
  dependsOn unzipKit

// Server-side
  def serverJvm = Jvm.current()
  environment 'JAVA_HOME', serverJvm.javaHome
  if (serverJvm.getJavaVersion().compareTo(JavaVersion.VERSION_1_8) > 0) {
    environment 'JAVA_OPTS', '--add-modules java.xml.bind'
  }

//If this directory does not exist, tests will fail with a cryptic assert failure
  systemProperty 'kitInstallationPath', "$unzipKit.destinationDir/${project(':terracotta-db').archivesBaseName}-$project.version"
//systemProperty 'serverDebugStartPort', "5005"

  testLogging {
    events 'started'
  }
}

// Helping out IDE
plugins.withType(IdeaPlugin) {
  sourceSets {
    test {
      compileClasspath += (findProject(':terracotta-common-client').configurations.compileOnly
          + findProject(':terracotta-ehcache-client').configurations.compileOnly
          + findProject(':terracotta-store-clustered-client').configurations.compileOnly
          + findProject(':multi-stripe:enterprise-voter').configurations.compileOnly)
    }
  }
}

eclipse {
  classpath {
    plusConfigurations += [ findProject(':terracotta-common-client').configurations.compileOnly,
                            findProject(':terracotta-ehcache-client').configurations.compileOnly,
                            findProject(':terracotta-store-clustered-client').configurations.compileOnly,
                            findProject(':multi-stripe:enterprise-voter').configurations.compileOnly ]
  }
}