import org.gradle.internal.jvm.Jvm

/*
 * Copyright (c) 2011-2019 Software AG, Darmstadt, Germany and/or Software AG USA Inc., Reston, VA, USA, and/or its subsidiaries and/or its affiliates and/or their licensors.
 * Use, reproduction, transfer, publication or disclosure is prohibited except as specifically provided for in your License Agreement with Software AG.
 */

dependencies {
  compile project(':utilities')
  compile "com.beust:jcommander:$jcommanderVersion"
  compile "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
  compileOnly "org.terracotta.internal:tc-config-parser:$terracottaConfigVersion"
  compileOnly "org.slf4j:slf4j-api:$slf4jVersion"
  compileOnly "org.terracotta.internal:dso-l2:$terracottaCoreVersion"

  testCompile "org.assertj:assertj-core:$assertjVersion"
  testCompile project(':galvan-support-ee')
  testCompile ("org.awaitility:awaitility:$awaitility")
  testCompile("com.github.stefanbirkner:system-rules:$systemRulesVersion") {
    exclude group: 'junit'
  }
}

apply plugin: TcDistribute

task unzipKit(type: Copy) {
  dependsOn project(':terracotta-db').distZip
  from zipTree(project(':terracotta-db').distZip.archivePath)
  into 'build/tc-db-kit'
}

test {
  dependsOn unzipKit

// Server-side
  def serverJvm = Jvm.current()
  environment 'JAVA_HOME', serverJvm.javaHome
  if (serverJvm.getJavaVersion().compareTo(JavaVersion.VERSION_1_8) > 0) {
    environment 'JAVA_OPTS', '--add-modules java.xml.bind'
  }

//If this directory does not exist, tests will fail with a cryptic assert failure
  systemProperty 'kitInstallationPath', "$unzipKit.destinationDir/${project(':terracotta-db').archivesBaseName}-$project.version"
//systemProperty 'serverDebugStartPort', "5005"

  testLogging {
    events 'started'
  }
}